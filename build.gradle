buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.12.1'
    }
}

apply plugin: 'com.android.library'
apply from: 'publish.gradle'


android {
    namespace 'com.shockwave.pdfium'
    compileSdkVersion 36

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 36
        versionCode 2
        versionName "2.0.0"
        buildConfigField "String", "VERSION_NAME", "\"${versionName}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    sourceSets{
        main {
            jni.srcDirs = []
            jniLibs.srcDir 'src/main/jni/lib'
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    ndkVersion '28.2.13676358'
    
    // Configure AAR filename to include version
    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "PdfiumAndroid-${defaultConfig.versionName}-${variant.buildType.name}.aar"
        }
    }
}

repositories {
    google()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.core:core:1.16.0'
}